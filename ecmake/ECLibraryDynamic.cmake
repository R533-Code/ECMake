include_guard(GLOBAL)

include(ECMake)
include(ECNamespace)
include(ECParseArgs)
include(GenerateExportHeader)

function(ec_add_library_dynamic NAME)
    ec_parse_with_defaults(LIB
        "VERSION;0.0.0.0;ROOT_DIR;${CMAKE_CURRENT_SOURCE_DIR};CXX_VERSION;20;INSTALL_BINDIR;${CMAKE_INSTALL_BINDIR};INSTALL_COMPONENT;Runtime" # defaults
        "NO_CONFIG;NO_INSTALL;NO_CONFORMANT_PREPROCESSOR_MSVC;NO_DEBUG_POSTFIX" # options
        "VERSION;ROOT_DIR;CXX_VERSION" # one value
        "" # multi value
        ${ARGN}
    )

    string(STRIP "${NAME}" NAME)

    ec_namespace_get(current_namespace "::")
    set(NAME_ALIAS "${current_namespace}${NAME}")
    message(VERBOSE "Creating dynamic library ${NAME_ALIAS} v${LIB_VERSION} with root dir `${LIB_ROOT_DIR}`, (C++${LIB_CXX_VERSION})...")

    ec_parse_version("${LIB_VERSION}" LIB_VERSION)

    file(GLOB_RECURSE _lib_cpp "${LIB_ROOT_DIR}/src/*.cpp")
    file(GLOB_RECURSE _lib_hpp "${LIB_ROOT_DIR}/src/*.h" "${LIB_ROOT_DIR}/src/*.hpp")

    string(REPLACE "::" "_" NAME_ALIAS_UNDERSCORES "${NAME_ALIAS}")
    string(TOLOWER "${NAME_ALIAS_UNDERSCORES}" _lib_name_lower)
    string(TOUPPER "${NAME_ALIAS_UNDERSCORES}" _lib_name_upper)

    # name of the export header generated by `generate_export_header`
    set(_lib_export_include "${_lib_name_lower}_export.h")

    # name of the export macro generated by `generate_export_header`
    set(_lib_export_prefix "${_lib_name_upper}")

    if(NOT LIB_NO_CONFIG)
        message(VERBOSE "Writing `${NAME}_config.h/cpp`...")
        ec_target_write_config("${NAME_ALIAS}" "${NAME}"
            ${LIB_VERSION_MAJOR} ${LIB_VERSION_MINOR} ${LIB_VERSION_PATCH} ${LIB_VERSION_TWEAK}
            "${CMAKE_CURRENT_BINARY_DIR}" "${_lib_export_prefix}_EXPORT" "${_lib_export_include}" _lib_extra
        )
    endif()

    add_library(${NAME} SHARED ${_lib_cpp} ${_lib_hpp} ${_lib_extra})
    add_library(${NAME_ALIAS} ALIAS ${NAME})

    # set properties for better defaults
    ec_target_set_default_properties(${NAME} ${LIB_CXX_VERSION}
        ${ARGN}
    )

    generate_export_header(
        ${NAME}
        EXPORT_FILE_NAME "${CMAKE_CURRENT_BINARY_DIR}/${_lib_export_include}"
        BASE_NAME "${_lib_export_prefix}"
    )
    target_include_directories(${NAME} SYSTEM PRIVATE "${CMAKE_CURRENT_BINARY_DIR}")

    if(NOT LIB_NO_INSTALL)
        include(GNUInstallDirs)

        # install the target
        install(TARGETS ${NAME}
            RUNTIME DESTINATION ${LIB_INSTALL_BINDIR}
            BUNDLE DESTINATION ${LIB_INSTALL_BINDIR}
            COMPONENT ${LIB_INSTALL_COMPONENT}
        )
    endif()

    message(VERBOSE "Created libutable ${NAME_ALIAS}.")
endfunction(ec_add_library_dynamic)

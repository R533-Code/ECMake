include_guard(GLOBAL)

include(ECMake)
include(ECNamespace)
include(ECParseArgs)

function(ec_add_executable NAME)
    ec_parse_with_defaults(EXEC
        "VERSION;0.0.0.0;ROOT_DIR;${CMAKE_CURRENT_SOURCE_DIR};CXX_VERSION;20" # defaults
        "NO_CONFIG" # options
        "VERSION;ROOT_DIR;CXX_VERSION" # one value
        "" # multi value
        ${ARGN}
    )

    string(STRIP "${NAME}" NAME)

    ec_namespace_get(current_namespace "::")
    set(NAME_ALIAS "${current_namespace}${NAME}")
    message(VERBOSE "Creating executable ${NAME_ALIAS} v${EXEC_VERSION} with root dir `${EXEC_ROOT_DIR}`, (C++${EXEC_CXX_VERSION})...")

    ec_parse_version("${EXEC_VERSION}" EXEC_VERSION)

    file(GLOB_RECURSE _exec_cpp "${EXEC_ROOT_DIR}/src/*.cpp")
    file(GLOB_RECURSE _exec_hpp "${EXEC_ROOT_DIR}/src/*.h" "${EXEC_ROOT_DIR}/src/*.hpp")

    add_executable(${NAME} ${_exec_cpp} ${_exec_h})
    add_executable(${NAME_ALIAS} ALIAS ${NAME})

    set_target_properties(${NAME} PROPERTIES
        CXX_STANDARD "${EXEC_CXX_VERSION}"
        CXX_STANDARD_REQUIRED TRUE
        CXX_VISIBILITY_PRESET hidden
        VISIBILITY_INLINES_HIDDEN hidden        
    )

    if (NOT (${EXEC_NO_CONFIG}))
        message(VERBOSE "Writing `${NAME}_config.h`...")
        ec_namespace_get(full_name "_")
        set(full_name "${full_name}${NAME}")
        string(TOUPPER "${full_name}" full_name)
        file(WRITE "${CMAKE_CURRENT_BINARY_DIR}/${NAME}_config.h"
            "// file generated by ECMake for target `${NAME_ALIAS}`\n"
            "#ifndef __ECMAKE_HG_${full_name}\n"
            "#define __ECMAKE_HG_${full_name}\n"
            "\n"
            "#define ${full_name}_VERSION_MAJOR ${EXEC_VERSION_MAJOR}\n"
            "#define ${full_name}_VERSION_MINOR ${EXEC_VERSION_MINOR}\n"
            "#define ${full_name}_VERSION_TWEAK ${EXEC_VERSION_TWEAK}\n"
            "#define ${full_name}_VERSION_PATCH ${EXEC_VERSION_PATCH}\n"
            "#define ${full_name}_VERSION_STRING \"${EXEC_VERSION_MAJOR}.${EXEC_VERSION_MINOR}.${EXEC_VERSION_PATCH}.${EXEC_VERSION_TWEAK}\"\n"
            "\n"
            "#define ${full_name}_TARGET_NAME \"${NAME}\"\n"
            "#define ${full_name}_TARGET_QUALIFIED_NAME \"${NAME_ALIAS}\"\n"
            "\n"
            "#define ${full_name}_CXX_VERSION ${EXEC_CXX_VERSION}\n"
            "#define ${full_name}_CXX_VERSION_STRING \"${EXEC_CXX_VERSION}\"\n"
            "\n"
            "#endif // !__ECMAKE_HG_${full_name}\n"
        )
    endif()

    if(MSVC)
        target_compile_options(${NAME} PRIVATE "/Zc:preprocessor")
        message(VERBOSE "Adding conformant preprocessor (for MSVC)")
    endif()

    message(VERBOSE "Created executable ${NAME_ALIAS}.")
endfunction(ec_add_executable)
